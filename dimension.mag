load "grundman-chi.mag";
// user functions:
// zeta      calculate the rational number zeta_K(-1)
// vt_series calculate the Hilbert series of the ring of modular forms

// make the zeta function rational
function zeta(F,n)
  error if n ge 0, "only works at negative integers";
  if not IsTotallyReal(F) or IsEven(n) then return 0; end if;
  zf := LSeries(F);
  zv := Evaluate(zf,n);
  mp := MinimalPolynomial(zv,1);
  val := Roots(mp,Q)[1,1];
  return val;
end function;

// from Vasquez-Thomas; maybe easier but only works in level 1
// note that this is all modular forms, so it should be 1 bigger than
// the Hilbert series for cusp forms
function vt_series(nf:bd := 20)
  Ql := PowerSeriesRing(Q,bd);
  if Discriminant(nf) eq 81 then ans := (1-l+l^2+l^3+l^4+6*l^5+4*l^6-2*l^7+4*l^8+6*l^9-l^10+3*l^11+3*l^12-3*l^13+l^14)/((1-l)^2*(1-l^2)*(1-l^9)) where l is Ql.1;
  elif Discriminant(nf) eq 49 then ans := (1+l^4+3*l^5+5*l^6+4*l^7+3*l^8+3*l^9+3*l^10+2*l^11-2*l^13+l^14)/((1-l)*(1-l^2)*(1-l^3)*(1-l^7)) where l is Ql.1;
  else
    d, zv, cl2, cl3, a2, a3, ch := chi(nf);
    h := ClassNumber(nf);
    hp := NarrowClassNumber(nf);
    bis := [1,h-ch-1,1/4*(-25*zv+3*a2/4+2*a3/3)-2-h,1/4*(-71*zv-3*a2/4+a3/3)+1-h,1/2*(-47*zv+a2/4-2*a3/3)+2];
    bis cat:= [bis[4]+2*h,bis[3]+2*h,-(h+ch+1),1];
    ans := (&+[bis[i]*l^(i-1): i in [1..9]]/((1-l)^2*(1-l^2)*(1-l^3))+O(l^bd)) where l is Ql.1;
  end if;
  return ans;
end function;

